; _/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/
;      基本設定 2023-08-17 tukasa
; _/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/

X_beep = B00000000 ; Beepを全く出さない
X_execs	= 0	; 初めて実行する実行可能ファイルを警告 0:しない 1:する
X_fstff	= 2	; 一覧取得高速化-Win7以降 0:しない 1:する(メモリ増) 2:1+SFN無
X_vzs	= 0	; 選択する範囲を選択方向に応じて変更 0:しない 1:する
XC_mvUD = 0,1,4,B0000,5,B010 ; [↑][↓] ; 上下カーソルで端まできたらループ
XV_drag = 2,1,3,0 ; ドラッグ開始ボタン スクロール,選択,窓移動,ジェスチャ(右のみ)
XC_acsr = 0,0 ; 各コマンド終了後のカーソル位置処理
X_rscah	= 4	; ローマ字検索キャッシュを行う最長文字数
XV_tmod	= 0	; テキスト表示時の標準は 0:ページモード 1:キャレットモード
XV_drag	= 0,2,3,0	; ドラッグ開始ボタン スクロール,選択,窓移動,ジェスチャ(右のみ)
		; 0:使用しない 1:左 2:右 3:中/ホイール 4:左右同時 5:第4 6:第5
X_term	= wt.exe	; PPbに使用するターミナル(例: wt.exe)
X_fles	= 1	; 画面のちらつき対策を 0:しない 1:する
XC_isea = 2,1,3,1,0 ; インクリメンタルサーチ動作 方法,位置,対象,ハイライト,ヒストリ保存
F_mes	= -19,0,0,0,400,0,0,0,128,3,2,1,50,源真ゴシックP Regular,96	; 汎用
XV_cols = { ; 表示桁数=該当拡張子、全てに一致しなければ80、0:桁数最大 -1:窓幅に合わせる
-1 = .txt,.howm,.ini
0 = .js,.cfg,.vbs
}
C_entry	= _DWHI,_DWHI,HFFFF80,_RED,HFFC020, H8000FF,HFF8080,H80FF00,_DWHI, _SBLU,H80FF00,HFFFF80,H0000C0,H80FFFF 
KC_main = {
E ,%Obs %'xyzzy' %FCD %: *fitwindow %N,%*findwindowtitle("-xyzzy-"),20
}

K_lied	= {
FIRSTEVENT	,*completelist -module:off
}

MV_click	= {	; PPv メイン窓
LD_SPC	= @Q
}

_others	= {	; その他設定
NewDir	= %*nowdatetime(Y-N-D-HMS)
shortedit	= 3
}
A_exec = { ; エイリアス
xyzzy = D:\bin\xyzzy\xyzzycli.exe
editor = D:\bin\xyzzy\xyzzycli.exe
7z	= "C:\Program Files\7-Zip\7z.exe"
dust	= D:\work\memo\Dust
}

; _/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/
;      一行編集でランチャ
; _/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/

_Command	= {	; ユーザコマンド・関数
ppl	= *string o,name=%*input("" -title:"PPlauncher" -mode:g -leavecancel -k:"*completelist /set /file:%%0launch.txt -match:6 -detail:""hist user"" ") %: *execute,%so"name"
}

KC_main = {
H	,*ppl
}

; _/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/
;      PPv一対一対応
; _/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/

_Command	= {	; ユーザコマンド・関数
myppv	= *ppv %*name(CD,"%R","%1") -k *string i,ppcid=%%*rightstr("%n", 1) %%: *fitwindow %N,%%N,20 %%: *fitwindow %N,%%N,30 %: *wait 300,2
}

E_cr = { ; [Enter]用判別
* ,*myppv
:JPEG ,
:BMP ,
:XJS ,
:XVBS ,
PNG ,*myppv
JPG ,*myppv
JPEG ,*myppv
BMP ,*myppv
GIF ,*myppv
TXT ,*myppv
CFG ,*myppv
HOWM ,*myppv
}

KV_main	= {	; PPvメイン窓
ENTER    = @Q
UP	,*ifmatch !0,0%si"ppcid" %: *execute C%si"ppcid",%%K"@UP" %: *ppv -bootid:%*rightstr("%n", 1) -r %*extract(C%si"ppcid""%(%*name(CD,"%R","%1")%)") %: *stop
	%K"@UP"
DOWN	,*ifmatch !0,0%si"ppcid" %: *execute C%si"ppcid",%%K"@DOWN" %: *ppv -bootid:%*rightstr("%n", 1) -r %*extract(C%si"ppcid""%(%*name(CD,"%R","%1")%)") %: *stop
	%K"@DOWN"
LEFT	,*ifmatch !0,0%si"ppcid" %: *execute C%si"ppcid",%%K"@LEFT" %: *ppv -bootid:%*rightstr("%n", 1) -r %*extract(C%si"ppcid""%(%*name(CD,"%R","%1")%)") %: *stop
	%K"@LEFT"
RIGHT	,*ifmatch !0,0%si"ppcid" %: *execute C%si"ppcid",%%K"@RIGHT" %: *ppv -bootid:%*rightstr("%n", 1) -r %*extract(C%si"ppcid""%(%*name(CD,"%R","%1")%)") %: *stop
	%K"@RIGHT"
SPACE    ,*ifmatch !0,0%si"ppcid" %: *execute C%si"ppcid",%%K"@SPACE" %: *ppv -bootid:%*rightstr("%n", 1) -r %*extract(C%si"ppcid""%(%*name(CD,"%R","%1")%)") %: *stop
	%K"@SPACE"
\SPACE    ,*ifmatch !0,0%si"ppcid" %: *execute C%si"ppcid",%%K"@\SPACE" %: *ppv -bootid:%*rightstr("%n", 1) -r %*extract(C%si"ppcid""%(%*name(CD,"%R","%1")%)") %: *stop
	%K"@\SPACE"
CLOSEEVENT    ,*ifmatch !0,0%si"ppcid" %: *focus C%si"ppcid" %: *stop
}

KV_crt	= {	; PPvテキスト(キャレット)追加設定
UP	,%K"@UP"
DOWN	,%K"@DOWN"
LEFT	,%K"@LEFT"
RIGHT	,%K"@RIGHT"
SPACE    ,%K"@SPACE"
\SPACE    ,%K"@\SPACE"
}

; _/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/
;      サブ窓の利用
; _/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/

_Command	= {	; ユーザコマンド・関数
opensubwin	= *ppc -r -bootid:x -single -k *jumppath %*arg(1) %%: *fitwindow %NC,%%NC,20 %%: *mapkey use,K_subwin %%: *js "PPx.EntryIndex = 2" %%: *linemessage %*arg(2)
}

KC_main    = {
\M ,*setcust _User:temp_exec=*file !move ,%%*extract(%n"%%%%@*8FCDN"),%%1 %%: %%K"@Q"
	*opensubwin %1,移動先のフォルダを選択してください
\C ,*setcust _User:temp_exec=*file !copy ,%%*extract(%n"%%%%@*8FCDN"),%%1 %%: %%K"@Q"
	*opensubwin %1,コピー先のフォルダを選択してください
\U ,*setcust _User:temp_exec=*script %0Script\wrap_7z.js,u,%%*extract(%n"%%%%a*8FCDN"),%%1 %%: %%K"@Q"
	 *opensubwin %FCD,展開先のフォルダを選択してください
\P ,*setcust _User:temp_exec=*script %0Script\wrap_7z.js,p,%%*extract(%n"%%%%a*8FCDN"),%(%*input("%1%*addchar(\)|%*extract(C"%%X")|.zip" -title:"7zipで圧縮" -select:i) %: %K"@Q"%)
	*opensubwin %FCD,圧縮先のフォルダを選択してください
}

K_subwin	= {
\ENTER    ,*execute ,%*getcust(_User:temp_exec)
}

; _/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/
;      一時コマンド
; _/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/

KC_main = {
^Z ,*setnextkey K_launcher
}

K_launcher = {
Z ,*setcust _User:temp_cmd = %"一時コマンド"%{%*getcust(_User:temp_cmd)%}
^Z ,*execute ,%*getcust(_User:temp_cmd)
}

; _/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/
;      moe
; _/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/

XC_cwrt	= 2	; コメントファイル作成は	0:手動 1:確認有 2:自動

_Command	= {	; ユーザコマンド・関数
todo	= *script %0Script\settitle.js,%1,%*input("[%*now(date)]- " -title:"todo" -select:l) %: *setcust _User:todo=0
done	= *script %0\Script\Done.js
}

KC_main    = {	; PPcメイン窓
^N ,%Ob,xyzzy -e (howm-create2dir) %1
F6    ,*script %0Script\title2comment_all.js %: *setcust XC_cwrt= 2
^R ,*comment "%ee%"コメントの編集"%{%*comment%|%}"
COMMENTEVENT2 ,*script %0Script\todoSort.js
D	,*ifmatch .howm %: *ppcfile !move,%'dust',/sameall:on /same:5 /querycreatedirectory:off %: *stop
	%K"@D
}

KV_page	= {	; PPvテキスト(ページ)追加設定
D ,*ifmatch %*extract(C"%%t"),howm %: *file !move,%*extract(C"%%R"),%'dust',/sameall:on /same:5 /querycreatedirectory:off %: *stop
 %KC"@D"
}


MC_celS    = {    ; エントリ表示 書式([;]メニュー)
howmtitle    = M cF25,6 w35C z7 S1 tC"y-N-D" s1
todo	= B cF20,6 w50C u2,5 s1
}

; _/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/
;      隠しメニュー
; _/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/

-|HM_ppv =

HM_ppv	= {	; PPv
T ^	, _AUTO,_DBLU = @^\NUM+
T v	, _AUTO,_DBLU = @^\NUM-
TopW	, _AUTO,_DBLU , *topmostwindow %N
pppv	, _AUTO,_DBLU , *RotateCustomize _others:pppv,1,0 %: %K"loadcust"
}

; _/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/
;      zoxide
; _/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/

KC_main = {
LOADEVENT ,*ifmatch "o:e,!a:a",%1 %: %Obcd zoxide add "%1"
'@'	,%Oq *run -noppb -pos:%*windowrect(,l),%*windowrect(,t) nyagos -f %0jumppath.lua %0%/
}

Mes0411	= {
EXEF	= 
}

; _/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/
;      マークの有無でCtrl+Wの検索対象を変化
; _/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/

KC_main	= {	; PPcメイン窓
^W	,*if %*js("PPx.result=PPx.EntryMarkCount") %: *where -marked:on %: *stop
	*where"
}

; _/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/
;      一行編集で音楽ファイル検索
; _/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/

_Command	= {
musicsearch	= *ifmatch 0,0%*getcust(_User:musicplayer) %: *setcust _User:musicplayer=%*input("D:\bin\AIMP5\AIMP.exe" -title:"音楽プレイヤーのパスを入力してください" -mode:e)
	*string o,name=%*input("" -title:"musicplayer - %*getcust(_User:musicplayer)" -mode:e -k:"*completelist /set /file:%%0data\\musicpath -match:6  %%: *mapkey use,K_musicplayer")
	*ifmatch !0,0%so"name" %: *launch %*getcust(_User:musicplayer) %so"name"
}

-|K_musicplayer=

K_musicplayer = {
ENTER	,*ifmatch -1,%*sendmessage(%N-L,392,0,0) %: %K"@DOWN @ENTER" %: *stop
	%K"@ENTER"
^O	,%M_musicplayer,P
^P	,%M_musicplayer,!START/STOP
^S	,*linemessage スキャン開始 %: *whereis -mask1:"a:d+;.mp3;.flac" -path:"D:\Data\Music" -dir:on -subdir:on -listfile:%0data\musicpath -name
LEFT	,*ifmatch !-1,%*sendmessage(%N-L,392,0,0) %: *replace %*name(D,%*edittext) %: %K"@^\A" %: *stop
	%K"@LEFT"
}

-|M_musicplayer=

M_musicplayer	= {
Playerの変更	=*setcust _User:musicplayer=%{%*getcust(_User:musicplayer)%|%} %: *setcaption musicplayer - %*getcust(_User:musicplayer)
AIMP5	=*setcust _User:musicplayer=D:\bin\AIMP5\AIMP.exe %: *setcaption musicplayer - %*getcust(_User:musicplayer)
AIMP5に追加	=*setcust _User:musicplayer=D:\bin\AIMP5\AIMP.exe /INSERT %: *setcaption musicplayer - %*getcust(_User:musicplayer)
--	= 
START/STOP	=D:\bin\AIMP5\AIMP.exe /PLAYPAUSE
--	= 
SCAN	=*whereis -mask1:"a:d+;.mp3;.flac" -path:"D:\Data\Music" -dir:on -subdir:on -listfile:%0data\musicpath -name
}

; _/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/
;      一行編集で漫画ファイル検索
; _/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/

_Command	= {
mangasearch	= *string o,name=%*input("" -title:"漫画ファイルを検索" -mode:e -k:"*completelist /set /file:%%0data\\mangapath -match:6 %%: *mapkey use,K_mangasearch")
	*ifmatch !0,0%so"name" %: *launch D:\bin\NeeView\NeeView.exe -n -o %*regexp("%so"name"","s/\\$//")
}

-|K_mangasearch=

K_mangasearch	= {
ENTER	,*ifmatch -1,%*sendmessage(%N-L,392,0,0) %: %K"@DOWN @ENTER" %: *stop
	%K"@ENTER"
^S	,*linemessage スキャン開始 %: *whereis -mask1:"a:d+" -path:"D:\Data\mangaZip" -dir:on -subdir:on -listfile:%0data\mangapath -name
}

; _/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/
;      一行編集で実行ファイル検索
; _/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/

_Command	= {
exesearch	= *string o,name=%*input("" -title:"実行ファイルを検索" -mode:g -leavecancel -k:"*completelist /set /file:%%0data\execpath -match:6 -detail:""user"" %%: *mapkey use,K_exesearch") %: *execute,%so"name"
}

-|K_exesearch=

K_exesearch = {
ENTER	,*ifmatch -1,%*sendmessage(%N-L,392,0,0) %: %K"@DOWN @ENTER" %: *stop
	%K"@ENTER"
^S	,*linemessage スキャン開始 %: *whereis -mask1:".exe" -path:"D:\bin" -dir:on -subdir:on -listfile:%0data\execpath -name
}

; _/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/
;      PPv起動用PPc
; _/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/

K_forppv    = {
ENTER    ,*ifmatch "o:e,a:d+","%*name(CD,"%R","%1")" %: %K"@ENTER" %: *stop
    *if %*js("PPx.result=PPx.SyncView") %: %Oi *windowsize %si"ppvid",%*windowrect(,w),%*calc("%*windowrect(,h)*2") %: *closeppx %n %: *stop
    %Oi *ppv %*name(CD,"%R","%1") -k *fitwindow %N,%%N,20 %%: *closeppx %n
\Y    ,*if %*js("PPx.result=PPx.SyncView") %: *ppvoption sync off %: *stop
    %Oi *ppv -k *setcust _User:id=%%*rightstr("%%n", 1) %: *fitwindow %N,%NV%*getcust(_User:id),30 %: *string i,ppvid=%NV%*getcust(_User:id) %: *ppvoption sync %*getcust(_User:id) %: *fitwindow %N,%NV%*getcust(_User:id),3
Q    ,*ppvoption sync off %: *closeppx %n
}

KV_main    = {    ; PPvメイン窓
O    ,%Oi *string i,ppcid=
 *windowsize %N,%*windowrect(,w),%*calc("%*windowrect(,h)/2")
 *ppc -r -bootid:v -single -k *jumppath %FD -entry:%FC %%: *string i,ppvid=%N %%: *mapkey use,K_forppv %%: *ppvoption sync %*rightstr("%n", 1)
 *fitwindow %N,%NCV,4
}

; _/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/
;      ハイライトモード
; _/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/

-|K_highlightmode =

K_highlightmode = {
SPACE	,*if !%si"hlnum" %: %K"@SPACE" %: *stop
	*if %*js("PPx.result=PPx.EntryHighlight") %: *markentry -highlight:0 path:,%R %: %K"@DOWN" %: *stop
	*markentry -highlight:%si"hlnum" path:,%R %: %K"@DOWN"
\SPACE	,*if !%si"hlnum" %: %K"@\SPACE" %: *stop
	*if %*js("PPx.result=PPx.EntryHighlight") %: *markentry -highlight:0 path:,%R %: %K"@UP" %: *stop
	*markentry -highlight:%si"hlnum" path:,%R %: %K"@UP"
^S	, *makelistfile 00_LISTFILE.txt -basic -highlight %: *linemessage ハイライトを保存しました
ESC	, *linecust highlight,KC_main:LOADEVENT, %: *mapkey delete,K_highlightmode %: *linemessage HIGHLIGHT MODE END
0	, *linemessage ファイルマークに変更しました %: *string i,hlnum=0
1	, *linemessage ハイライト1に変更しました %: *string i,hlnum=1
2	, *linemessage ハイライト2に変更しました %: *string i,hlnum=2
3	, *linemessage ハイライト3に変更しました %: *string i,hlnum=3
4	, *linemessage ハイライト4に変更しました %: *string i,hlnum=4
5	, *linemessage ハイライト5に変更しました %: *string i,hlnum=5
6	, *linemessage ハイライト6に変更しました %: *string i,hlnum=6
7	, *linemessage ハイライト7に変更しました %: *string i,hlnum=7
}